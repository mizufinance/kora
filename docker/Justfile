set dotenv-load := true

default:
    @just --list

build:
    docker buildx bake --allow=fs.read=.. -f docker-bake.hcl kora-local

build-fresh:
    docker buildx bake --allow=fs.read=.. -f docker-bake.hcl kora-local --no-cache

# Start devnet with interactive DKG (production-like)
devnet:
    ./scripts/devnet-run.sh --interactive-dkg

# Start devnet with trusted dealer DKG (fast, insecure, for local dev)
trusted-devnet:
    ./scripts/devnet-run.sh

devnet-minimal:
    @COMPOSE_PROFILES="" ./scripts/devnet-run.sh

stats:
    ./scripts/devnet-stats.sh

down:
    docker compose -f compose/devnet.yaml down

reset:
    docker compose -f compose/devnet.yaml down -v
    @echo "Devnet reset. Next 'just devnet' runs fresh DKG."

restart: down devnet

restart-validators:
    docker compose -f compose/devnet.yaml restart validator-node0 validator-node1 validator-node2 validator-node3

status:
    @echo ""
    @docker compose -f compose/devnet.yaml ps
    @echo ""
    @echo "Endpoints:"
    @echo "  P2P:        localhost:30400-30403"
    @echo "  RPC:        localhost:8540-8543 (disabled)"
    @echo "  Prometheus: http://localhost:9090"
    @echo "  Grafana:    http://localhost:3000"

logs:
    docker compose -f compose/devnet.yaml logs -f validator-node0 validator-node1 validator-node2 validator-node3

logs-dkg:
    docker compose -f compose/devnet.yaml --profile interactive-dkg logs -f dkg-node0 dkg-node1 dkg-node2 dkg-node3

logs-node node:
    docker compose -f compose/devnet.yaml logs -f {{node}}

exec node:
    docker compose -f compose/devnet.yaml exec {{node}} /bin/bash

redo-dkg:
    #!/bin/bash
    echo "Stopping validators..."
    docker compose -f compose/devnet.yaml stop validator-node0 validator-node1 validator-node2 validator-node3
    
    echo "Removing old DKG shares..."
    for i in 0 1 2 3; do
        docker run --rm -v kora-devnet_data_node${i}:/data alpine rm -f /data/share.key /data/output.json
    done
    
    echo "Starting interactive DKG ceremony..."
    docker compose -f compose/devnet.yaml --profile interactive-dkg up -d dkg-node0 dkg-node1 dkg-node2 dkg-node3
    
    echo "Waiting for DKG ceremony to complete..."
    timeout=300
    while true; do
        EXITED=$(docker compose -f compose/devnet.yaml ps --format json 2>/dev/null | \
            jq -r 'select(.Service | startswith("dkg-")) | select(.State == "exited") | select(.ExitCode == 0) | .Service' 2>/dev/null | wc -l | tr -d ' ')
        FAILED=$(docker compose -f compose/devnet.yaml ps --format json 2>/dev/null | \
            jq -r 'select(.Service | startswith("dkg-")) | select(.State == "exited") | select(.ExitCode != 0) | .Service' 2>/dev/null | wc -l | tr -d ' ')
        
        [[ "$FAILED" -gt 0 ]] && echo "DKG ceremony failed!" && exit 1
        [[ "$EXITED" -ge 4 ]] && break
        
        timeout=$((timeout - 2))
        [[ $timeout -le 0 ]] && echo "DKG timeout!" && exit 1
        sleep 2
    done
    
    docker compose -f compose/devnet.yaml --profile interactive-dkg stop dkg-node0 dkg-node1 dkg-node2 dkg-node3 2>/dev/null || true
    docker compose -f compose/devnet.yaml up -d validator-node0 validator-node1 validator-node2 validator-node3
    echo "DKG complete. Validators restarted."

lint:
    hadolint Dockerfile

validate:
    docker compose -f compose/devnet.yaml config --quiet && echo "Compose valid"
